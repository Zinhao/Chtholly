plugins {
    id 'com.android.application'
}

android {
    compileSdk 34

    def date = new Date()
    def formatDate = date.format("yyyyMMdd")
    signingConfigs {
        keystore {
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file("local.properties").newDataInputStream()
            properties.load(inputStream)
            def storeFilePath = properties.getProperty("key.file")
            storeFile file(storeFilePath)
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }
    defaultConfig {
        applicationId "com.zinhao.chtholly"
        minSdk 27
        //noinspection EditedTargetSdkVersion
        targetSdk 34
        versionCode 17
        versionName "1.3.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProviders(
                        new RoomSchemaArgProvider(new File(projectDir, "schemas"))
                )
            }
        }
        signingConfig signingConfigs.keystore
    }



    flavorDimensions("default")
    productFlavors{
        Chtholly{
            applicationId = "com.zinhao.chtholly"
            signingConfig signingConfigs.keystore
            dimension 'default'
        }
        NekoAi{
            applicationId = "com.zinhao.nekoai"
            signingConfig signingConfigs.keystore
            dimension 'default'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Android_${variant.name}_${variant.versionCode}_${variant.versionName}_${formatDate}.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.zinhao.chtholly'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-guava:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    implementation 'androidx.media3:media3-exoplayer:1.4.1'
    implementation 'androidx.media3:media3-exoplayer-dash:1.4.1'
    implementation 'androidx.media3:media3-ui:1.4.1'
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KSP, you should change the line below to return
        // ["room.schemaLocation=${schemaDir.path}"]
        return ["-Aroom.schemaLocation=${schemaDir.path}"]
    }
}